grammar game.of.life.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.of.game/life/DSL"

Game: decls += Decl*
	  grid = Grid
	  rules = Rules;

Decl: pat=CustomPattern;


Grid:{Grid} 'Grid' size=Point?
	 (marks += Mark* | fill = Fill);
	  
Mark: (pat=Pattern | 'point' poi=Point);

Pattern: (name=Predefined | custom=[CustomPattern]) loc=Point;

Predefined: ('Block' | 'Beehive' | 'Loaf' | 'Boat' | 'Tub' | 'Blinker' | 'Toad' | 'Beacon' |
	         'Pulsar'| 'Pentadecathlon' | 'Glider' | 'LWSS' | 'MWSS' | 'HWSS' | 'GosperGun' |
	         'SimkinGun' | 'R-pentonimo' | 'Diehard' | 'Acorn' );

CustomPattern: 'Pattern' name = ID '[' pointList += Point+ ']';

Fill: 'fill' percent=INT;

Point: '(' x=INT',' y=INT')';


Rules: 'Rules'
	   rules += (Rule)+;

Rule: 'Any' state=State 'cell' 'with' op=BoolOp nCount=INT 'neighbours' result=Result;

State: 'live' | 'dead';
	   
BoolOp: '>' | '<' | '>='| '<=';
		
Result: 'lives' | 'dies';

